#!/usr/bin/expect

set timeout 20
set filename [lindex $argv 0]

spawn telnet localhost 4444
expect {
	"Open On-Chip Debugger" { }
	"Connection closed by foreign host." {
		send_user "\nCould not connect!\n"
		exit 1
	}
}
expect ">"
send "reset halt\n"
expect {
	"target state: halted" { }
	"target halted due to debug-request" { }
	"jtag status contains invalid mode value" {
		send_user "\nFailed to reset device!\n"
		exit 1
	}
	">" {
		send "reset halt\n"
		expect {
			"target state: halted" { }
			"target halted due to debug-request" { }
			">" {
				send_user "\nFailed to reset device!\n"
				exit 1
			}
		}
	}
}
expect ">"
send "sleep 100\n"
expect ">"
send "stm32f4x.cpu curstate\n"
expect {
	"halted" { }
	"reset" {
		send_user "\nInvalid CPU state detected!\n"
		exit 1
	}
	"running" {
		send_user "\nInvalid CPU state detected!\n"
		exit 1
	}
}
expect ">"
send "flash probe 0\n"
expect {
	"flash 'stm32f2x' found at 0x08000000" { }
	">" {
		send_user "\nFlash bank not detected properly!\n"
		exit 1
	}
}
expect ">"
send "flash write_image erase $filename 0x08000000\n"
expect "auto erase enabled"
expect {
	"target halted due to breakpoint" { }
	"error*" {
		send_user "\nError writing flash!\n"
		exit 1
	}
	"flash write algorithm aborted by target" {
		send_user "\nFlashing aborted through device reset!\n"
		exit 1
	}
}
expect "wrote"
expect ">"
send_user "Checking if flash was successful and resetting target...\n"
send "reset run\n"
expect ">"
send "sleep 100\n"
expect ">"
send "stm32f4x.cpu curstate\n"
expect {
	"running" { }
	"reset" { }
	"halted" {
		send_user "\nInvalid CPU state detected!\n"
		exit 1
	}
}
expect ">"
send "exit\n"
expect "Connection closed by foreign host."
send_user "\nFlashed target successfully.\n"
exit 0
